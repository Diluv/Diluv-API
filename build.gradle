plugins {
    id 'java'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.1.1'
    //    id "checkstyle"
}

version = "${project_version}" + (isSnapshot() ? '.' + getBuildNumber() : '')

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation project('Confluencia')

    implementation group: 'com.diluv.schoomp', name: 'Schoomp', version: '1.2.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.1'

    implementation group: 'commons-io', name: 'commons-io', version: '2.9.0'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    implementation group: 'org.sejda.imageio', name: 'webp-imageio', version: '0.1.6'

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.10'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
    implementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'

    implementation group: 'com.github.slugify', name: 'slugify', version: '2.5'
    implementation group: 'com.wildbit.java', name: 'postmark', version: '1.7'

    implementation group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.2.3"
    implementation group: 'com.graphql-java-kickstart', name: 'graphql-java-tools', version: '11.0.1'
    implementation group: 'com.graphql-java', name: 'graphql-java', version: '16.2'

    implementation group: 'org.jboss.resteasy', name: 'resteasy-undertow', version: '4.6.0.Final'
    implementation group: 'org.jboss.resteasy', name: 'resteasy-multipart-provider', version: '4.6.0.Final'
    implementation group: 'org.jboss.resteasy', name: 'resteasy-atom-provider', version: '4.6.0.Final'
    implementation group: 'org.jboss.resteasy', name: 'resteasy-cdi', version: '4.6.0.Final'
    implementation group: 'org.jboss.resteasy', name: 'resteasy-validator-provider', version: '4.6.0.Final'
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.2.0.Final'
    implementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    implementation group: 'org.jboss.weld.servlet', name: 'weld-servlet', version: '2.4.8.Final'

    compileOnly group: 'org.hibernate', name: 'hibernate-core', version: '5.5.3.Final'

    testImplementation project(path: ':Confluencia', configuration: 'testOutput')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.2'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    testImplementation group: 'io.rest-assured', name: 'json-schema-validator', version: '4.4.0'

    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.15.3'
    testImplementation group: 'org.testcontainers', name: 'mariadb', version: '1.15.3'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.15.3'
}

test {
    environment "ENVIRONMENT", 'DEVELOPMENT'
    useJUnitPlatform()
}

jar {
    manifest {
        attributes([
            'Timestamp'               : System.currentTimeMillis(),
            'Specification-Title'     : project.archivesBaseName,
            'Specification-Vendor'    : project.vendor,
            'Specification-Version'   : project.version,
            'Implementation-Title'    : project.archivesBaseName,
            'Implementation-Version'  : project.version,
            'Implementation-Vendor'   : project.vendor,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
            'Main-Class'              : 'com.diluv.api.Main'
        ])
    }
}

jib {
    to {
        image = "diluv/diluv-api:" + version
        tags = ['latest' + (isSnapshot() ? "-snapshot" : '')]
        auth {
            username = System.getenv("DOCKER_USERNAME") ?: ''
            password = System.getenv("DOCKER_PASSWORD") ?: ''
        }
    }
}

//checkstyle {
//    configFile = file("checkstyle.xml")
//    toolVersion = '8.10'
//}

static String getBuildNumber() {
    return System.getenv("GITHUB_RUN_NUMBER") ?: "0"
}

static boolean isSnapshot() {
    String ref = System.getenv("GITHUB_REF");
    if (ref != null && ref.startsWith("refs/tags/v")) {
        return false
    }
    return true
}

task depsize {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }
            .each {
                out << "${it.name}".padRight(45)
                out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
            }
        println(out)
    }
}